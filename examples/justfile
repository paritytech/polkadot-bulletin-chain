# Polkadot Bulletin Chain - Just Commands
# 
# See README.md for usage instructions.

# Default recipe - run the complete PAPI workflow test
default: (run-authorize-and-store "bulletin-westend-runtime" "ws")

# Setup prerequisites for parachain runtime (polkadot and polkadot-omni-node binaries)
# This recipe clones polkadot-sdk, builds required binaries, and copies them to ~/local_bulletin_testing/bin
setup-parachain-prerequisites:
    #!/usr/bin/env bash
    set -e
    if [ -n "$SKIP_PARACHAIN_SETUP" ]; then
        echo "â­ï¸  Skipping parachain prerequisites (SKIP_PARACHAIN_SETUP set)"
    else
        ROOT_DIR="{{ justfile_directory() }}/.."
        cd "$ROOT_DIR"
        ./scripts/setup_parachain_prerequisites.sh
        cd -
    fi

compute-test-dir:
    #!/usr/bin/env bash
    if [ -n "$TEST_DIR" ]; then
        echo "$TEST_DIR";
    else
        TEST_DIR="$(mktemp -d /tmp/bulletin-tests-run-XXXXX)/test";
        mkdir -p "$TEST_DIR";
        echo "$TEST_DIR";
    fi

# Install JavaScript dependencies
npm-install:
    npm install

# Helper function to get IPFS command (internal use)
_ipfs-cmd:
    #!/usr/bin/env bash
    if [ -f "../kubo/ipfs" ]; then
        echo "../kubo/ipfs"
    elif command -v ipfs &> /dev/null; then
        echo "ipfs"
    else
        echo "âŒ Error: IPFS not found." >&2
        exit 1
    fi

kill-pids test_dir:
    #!/usr/bin/env bash
    set -e

    # Kill any existing background processes using stored PIDs
    echo "ğŸ” Checking for existing background processes - PIDs stored in {{ test_dir }}..."
    for pidfile in {{ test_dir }}/*.pid; do
        if [ -f "$pidfile" ]; then
            PID=$(cat "$pidfile")
            SERVICE=$(basename "$pidfile" .pid)
            if kill -0 $PID 2>/dev/null; then
                echo "   Found existing $SERVICE process (PID: $PID), pidfile: $pidfile"
                # Kill the process and its children
                pkill -TERM -P $PID 2>/dev/null || true
                sleep 2
                pkill -9 -P $PID 2>/dev/null || true
                sleep 2
                kill -9 $PID 2>/dev/null || true
                echo "   âœ“ Cleaned up $SERVICE process"
            else
                echo "   Stale $SERVICE PID file: $pidfile found (process not running)"
            fi
            rm "$pidfile"
        fi
    done

# Start solo chain or parachain with zombienet in background
# Parameters:
#   runtime - Runtime name (e.g., "bulletin-polkadot-runtime"), taken from ../scripts/runtimes-matrix.json
bulletin-solo-zombienet-start test_dir runtime="bulletin-polkadot-runtime":
    #!/usr/bin/env bash
    set -e

    # Find zombienet binary (use env var if set, otherwise search)
    if [ -n "$ZOMBIENET_BINARY" ]; then
        ZOMBIENET_BIN="$ZOMBIENET_BINARY"
    else
        ZOMBIENET_BIN=$(ls ../zombienet-*-* 2>/dev/null | head -n 1)
    fi
    
    if ! command -v "$ZOMBIENET_BIN" >/dev/null 2>&1; then
        echo "âŒ Error: Zombienet binary not found"
        echo "   Tried: $ZOMBIENET_BIN"
        echo "   Set ZOMBIENET_BINARY environment variable or ensure zombienet-* exists in parent directory"
        exit 1
    fi

    echo "   Setting up Polkadot runtime..."
    ROOT_DIR="{{ justfile_directory() }}/.."
    # Build the node binary (which will build the runtime as a dependency)
    cargo build --release -p polkadot-bulletin-chain
    POLKADOT_BULLETIN_BINARY_PATH="$ROOT_DIR/target/release/polkadot-bulletin-chain"

    cat <<EOF
    âš¡ Starting Bulletin chain with zombienet
        runtime   : {{ runtime }}
        test_dir  : {{ test_dir }}
        zombienet : $ZOMBIENET_BIN
        POLKADOT_BULLETIN_BINARY_PATH : $POLKADOT_BULLETIN_BINARY_PATH
    EOF
    cd "$ROOT_DIR"
    (yes y | POLKADOT_BULLETIN_BINARY_PATH="$POLKADOT_BULLETIN_BINARY_PATH" \
        $ZOMBIENET_BIN -p native spawn --dir {{ test_dir }} "$ROOT_DIR/zombienet/bulletin-polkadot-local.toml" > {{ test_dir }}/zombienet.log 2>&1) &
    cd -

    ZOMBIENET_PID=$!
    echo $ZOMBIENET_PID > {{ test_dir }}/zombienet.pid
    echo "   Zombienet PID: $ZOMBIENET_PID"
    echo "   Log: {{ test_dir }}/zombienet.log"
    # TODO: replace with better check for produced/finalized block 1 with timeout 60s
    echo "   Waiting for chain to start (60 seconds)..."
    sleep 60

# Start westend parachain with zombienet in background
# Parameters:
#   runtime - Runtime name (e.g., "bulletin-polkadot-parachain-runtime", "bulletin-westend-runtime"), taken from ../scripts/runtimes-matrix.json
bulletin-westend-parachain-zombienet-start test_dir runtime="bulletin-westend-runtime":
    #!/usr/bin/env bash
    set -e

    # Find zombienet binary (use env var if set, otherwise search)
    if [ -n "$ZOMBIENET_BINARY" ]; then
        ZOMBIENET_BIN="$ZOMBIENET_BINARY"
    else
        ZOMBIENET_BIN=$(ls ../zombienet-*-* 2>/dev/null | head -n 1)
    fi

    if ! command -v "$ZOMBIENET_BIN" >/dev/null 2>&1; then
        echo "âŒ Error: Zombienet binary not found"
        echo "   Tried: $ZOMBIENET_BIN"
        echo "   Set ZOMBIENET_BINARY environment variable or ensure zombienet-* exists in parent directory"
        exit 1
    fi

    # Run prerequisites setup first
    just setup-parachain-prerequisites

    # Set paths to binaries
    if [ -n "$SKIP_PARACHAIN_SETUP" ]; then
        # Expect in the PATH
        POLKADOT_BINARY_PATH=polkadot
        POLKADOT_PARACHAIN_BINARY_PATH=polkadot-omni-node
    else
        POLKADOT_BINARY_PATH=~/local_bulletin_testing/bin/polkadot
        POLKADOT_PARACHAIN_BINARY_PATH=~/local_bulletin_testing/bin/polkadot-omni-node
    fi

    # Verify binaries exist after setup
    if ! command -v "$POLKADOT_BINARY_PATH"; then
        echo "âŒ Error: Prerequisites setup completed but binaries still not found"
        echo "   Expected POLKADOT_BINARY_PATH: $POLKADOT_BINARY_PATH"
        exit 1
    fi
    if ! command -v "$POLKADOT_PARACHAIN_BINARY_PATH"; then
        echo "âŒ Error: Prerequisites setup completed but binaries still not found"
        echo "   Expected POLKADOT_PARACHAIN_BINARY_PATH: $POLKADOT_PARACHAIN_BINARY_PATH"
        exit 1
    fi

    # Create chain spec
    echo "   Creating Westend chain spec..."
    # Ensure binaries are in PATH and run from root directory
    ROOT_DIR="{{ justfile_directory() }}/.."
    cd "$ROOT_DIR"
    PATH="$HOME/local_bulletin_testing/bin:$PATH" ./scripts/create_bulletin_westend_spec.sh
    cd -

    # Run zombienet from root directory so relative paths in config work correctly
    cat <<EOF
    âš¡ Starting Bulletin chain with zombienet
        runtime   : {{ runtime }}
        test_dir  : {{ test_dir }}
        zombienet : $ZOMBIENET_BIN
        POLKADOT_BINARY_PATH : $POLKADOT_BINARY_PATH
        POLKADOT_PARACHAIN_BINARY_PATH : $POLKADOT_PARACHAIN_BINARY_PATH
    EOF
    cd "$ROOT_DIR"
    (yes y | POLKADOT_BINARY_PATH=$POLKADOT_BINARY_PATH \
        POLKADOT_PARACHAIN_BINARY_PATH=$POLKADOT_PARACHAIN_BINARY_PATH \
        $ZOMBIENET_BIN -p native spawn --dir {{ test_dir }} "$ROOT_DIR/zombienet/bulletin-westend-local.toml" > {{ test_dir }}/zombienet.log 2>&1) &
    cd -

    ZOMBIENET_PID=$!
    echo $ZOMBIENET_PID > {{ test_dir }}/zombienet.pid
    echo "   Zombienet PID: $ZOMBIENET_PID"
    echo "   Log: {{ test_dir }}/zombienet.log"
    # TODO: replace with better check for produced/finalized block 1 with timeout 180s
    echo "   Waiting for chain to start (180 seconds)..."
    sleep 180

# Check if Docker is available and running
_check-docker:
    #!/usr/bin/env bash
    if ! command -v docker &> /dev/null; then
        echo "âŒ Error: Docker is not installed."
        echo "   Please install Docker Desktop from: https://www.docker.com/products/docker-desktop"
        exit 1
    fi
    
    if ! docker ps &> /dev/null; then
        echo "âŒ Error: Docker is not running."
        echo "   Please start Docker Desktop and try again."
        echo "   On macOS: Open Docker Desktop from Applications"
        exit 1
    fi

# Start IPFS Docker container
ipfs-start test_dir: _check-docker
    #!/usr/bin/env bash
    set -e
    echo "ğŸ³ Starting IPFS Docker container..."
    
    # Pull latest kubo image if not present
    docker pull ipfs/kubo:latest
    
    # Determine network mode based on OS
    if [[ "$OSTYPE" == "darwin"* ]]; then
        # macOS - use port mapping
        NETWORK_ARGS="-p 4001:4001 -p 8080:8080 -p 5001:5001"
    else
        # Linux (including CI) - use host networking for direct access
        NETWORK_ARGS="--network host -p 4001:4001 -p 8080:8080 -p 5001:5001"
    fi
    
    # Start Docker container
    docker run -d --name ipfs-node -v ipfs-data:/data/ipfs $NETWORK_ARGS ipfs/kubo:latest
    echo "   Container: ipfs-node"
    echo "   Waiting for container to start..."
    sleep 5

    # Bitswap logging
    docker logs -f ipfs-node > {{ test_dir }}/ipfs-node.log 2>&1 &
    docker exec ipfs-node ipfs log level bitswap debug
    docker exec ipfs-node ipfs log level bitswap/client debug

    # Store container name in PID directory for cleanup
    echo "ipfs-node" > {{ test_dir }}/ipfs-docker.container

# Connect to IPFS nodes using Docker
ipfs-connect runtime:
    #!/usr/bin/env bash
    set -e
    
    echo "ğŸ”— Connecting IPFS nodes (Docker, runtime: {{ runtime }})..."
    
    # Detect the correct host and protocol for Docker
    if [[ "$OSTYPE" == "darwin"* ]]; then
        # macOS - use dns4/host.docker.internal
        PROTOCOL="dns4"
        DOCKER_HOST="host.docker.internal"
    else
        # Linux (with host networking) - use ip4/127.0.0.1
        PROTOCOL="ip4"
        DOCKER_HOST="127.0.0.1"
    fi
    
    echo "   Using Docker host: /$PROTOCOL/$DOCKER_HOST"

    # TODO: improve this for multiple runtimes
    # Different peer IDs for different runtimes
    if [ "{{ runtime }}" = "bulletin-westend-runtime" ]; then
        # Westend parachain peer IDs
        docker exec ipfs-node ipfs swarm connect /$PROTOCOL/$DOCKER_HOST/tcp/10001/ws/p2p/12D3KooWJKVVNYByvML4Pgx1GWAYryYo6exA68jQX9Mw3AJ6G5gQ || true
        docker exec ipfs-node ipfs swarm connect /$PROTOCOL/$DOCKER_HOST/tcp/12347/ws/p2p/12D3KooWJ8sqAYtMBX3z3jy2iM98XGLFVzVfUPtmgDzxXSPkVpZZ || true
    elif [ "{{ runtime }}" = "bulletin-polkadot-runtime" ]; then
        # Polkadot solo chain peer IDs
        docker exec ipfs-node ipfs swarm connect /$PROTOCOL/$DOCKER_HOST/tcp/10001/ws/p2p/12D3KooWQCkBm1BYtkHpocxCwMgR8yjitEeHGx8spzcDLGt2gkBm || true
        docker exec ipfs-node ipfs swarm connect /$PROTOCOL/$DOCKER_HOST/tcp/12347/ws/p2p/12D3KooWRkZhiRhsqmrQ28rt73K7V3aCBpqKrLGSXmZ99PTcTZby || true
    else
        echo "ğŸ³ Unhandled runtime: {{ runtime }} specified!"
        exit 1
    fi

# Shutdown IPFS Docker container
ipfs-shutdown test_dir:
    #!/usr/bin/env bash
    
    echo "ğŸ›‘ Shutting down IPFS Docker container..."
    docker stop ipfs-node 2>/dev/null || echo "   âš  Container not running or already stopped"
    docker rm ipfs-node 2>/dev/null || echo "   âš  Container not found or already removed"
    docker volume rm ipfs-data 2>/dev/null || echo "   âš  Volume not found or already removed"
    
    # Clean up container file if it exists
    [ -f "{{ test_dir }}/ipfs-docker.container" ] && rm "{{ test_dir }}/ipfs-docker.container" && echo "   âœ“ Cleaned up container file" || true

# Start IPFS reconnect script in background (Docker mode)
ipfs-reconnect-start test_dir runtime:
    #!/usr/bin/env bash
    set -e
    echo "ğŸ”„ Starting IPFS reconnect script (Docker mode, runtime: {{ runtime }})..."
    
    # Use different reconnect script for westend
    ROOT_DIR="{{ justfile_directory() }}/.."
    cd "$ROOT_DIR"
    # TODO: improve this for multiple runtimes
    if [ "{{ runtime }}" = "bulletin-westend-runtime" ]; then
        ./scripts/ipfs-reconnect-westend.sh docker > {{ test_dir }}/ipfs-reconnect.log 2>&1 &
    elif [ "{{ runtime }}" = "bulletin-polkadot-runtime" ]; then
        ./scripts/ipfs-reconnect-solo.sh docker > {{ test_dir }}/ipfs-reconnect.log 2>&1 &
    else
        echo "ğŸ³ Unhandled runtime: {{ runtime }} specified!"
        exit 1
    fi
    cd -
    
    RECONNECT_PID=$!
    echo $RECONNECT_PID > {{ test_dir }}/ipfs-reconnect-docker.pid
    echo "   Reconnect PID: $RECONNECT_PID"
    echo "   Log: {{ test_dir }}/ipfs-reconnect.log"
    sleep 2

# Generate PAPI descriptors
# Generate PAPI descriptors from a running node
# Parameters:
#   ws_url - WebSocket URL of the node (default: ws://localhost:10000)
papi-generate ws_url="ws://localhost:10000":
    #!/usr/bin/env bash
    set -e

    echo "ğŸ”§ Generating PAPI descriptors from {{ ws_url }}..."
    npx papi add -w "{{ ws_url }}" bulletin

# Setup all services using Docker for IPFS
# Parameters:
#   runtime - Runtime to build and run (e.g., "bulletin-polkadot-runtime", "bulletin-westend-runtime")
setup-services test_dir runtime:
    #!/usr/bin/env bash
    set -e

    # TODO: improve this for multiple runtimes
    if [ "{{ runtime }}" = "bulletin-westend-runtime" ]; then
        just bulletin-westend-parachain-zombienet-start "{{ test_dir }}" "{{ runtime }}"
    elif [ "{{ runtime }}" = "bulletin-polkadot-runtime" ]; then
        just bulletin-solo-zombienet-start "{{ test_dir }}" "{{ runtime }}"
    else
        echo "ğŸ³ Unhandled runtime: {{ runtime }} specified!"
        exit 1
    fi

    # Generate PAPI descriptors from running node.
    just papi-generate

    # Start IPFS
    echo "ğŸ”§ Tearing down Docker services if they are running..."
    just ipfs-shutdown "{{ test_dir }}"
    echo "ğŸ³ Setting up services with Docker IPFS (runtime: {{ runtime }})..."
    just ipfs-start "{{ test_dir }}"
    just ipfs-connect "{{ runtime }}"
    just ipfs-reconnect-start "{{ test_dir }}" "{{ runtime }}"

    echo "âœ… Services setup complete (Docker mode)"

# Stop all Docker services
teardown-services test_dir:
    #!/usr/bin/env bash

    # Stop all background processes
    just kill-pids "{{ test_dir }}"

    # Stop Docker container
    echo "ğŸ§¹ Stopping all Docker services..."
    just ipfs-shutdown "{{ test_dir }}"
    echo "âœ… Docker services stopped"

# Start all services for a runtime (use with run-test-* recipes)
# Usage: just start-services /tmp/my-test-dir bulletin-polkadot-runtime
start-services test_dir runtime: npm-install
    #!/usr/bin/env bash
    set -e
    mkdir -p "{{ test_dir }}"
    just setup-services "{{ test_dir }}" "{{ runtime }}"

# Stop all services
# Usage: just stop-services /tmp/my-test-dir
stop-services test_dir:
    #!/usr/bin/env bash
    echo "ğŸ“ Stopping services - test directory: {{ test_dir }}"
    just teardown-services "{{ test_dir }}"
    echo "âœ… Services stopped"

# Run authorize-and-store test only (services must already be running via start-services)
# Parameters:
#   test_dir - Test directory where services are running
#   runtime - Runtime name for smoldot chainspec path resolution
#   mode - Connection mode: "ws" (WebSocket RPC node) or "smoldot" (light client)
run-test-authorize-and-store test_dir runtime mode="ws":
    #!/usr/bin/env bash
    set -e

    if [ "{{ mode }}" = "smoldot" ]; then
        echo "ğŸ§ª Running authorize and store test (mode: smoldot, runtime: {{ runtime }})..."
        SCRIPT_NAME="authorize_and_store_papi_smoldot.js"
    elif [ "{{ mode }}" = "ws" ]; then
        echo "ğŸ§ª Running authorize and store test (mode: ws, runtime: {{ runtime }})..."
        SCRIPT_NAME="authorize_and_store_papi.js"
    else
        echo "âŒ Error: Invalid mode '{{ mode }}'. Must be 'ws' or 'smoldot'"
        exit 1
    fi

    # Run the script with chainspec_path parameter only for smoldot mode
    if [ "{{ mode }}" = "smoldot" ]; then
        if [ "{{ runtime }}" = "bulletin-westend-runtime" ]; then
            RELAY_CHAINSPEC_PATH="{{ test_dir }}/bob/cfg/westend-local.json"
            PARACHAIN_CHAINSPEC_PATH="{{ test_dir }}/bulletin-westend-collator-2/cfg/westend-local-1006.json"
            node $SCRIPT_NAME "$RELAY_CHAINSPEC_PATH" "$PARACHAIN_CHAINSPEC_PATH"
        else
            CHAINSPEC_PATH="{{ test_dir }}/bob/cfg/bulletin-polkadot-local.json"
            node $SCRIPT_NAME "$CHAINSPEC_PATH"
        fi
    else
        node $SCRIPT_NAME
    fi

# Run store-chunked-data test only (services must already be running via start-services)
# Parameters:
#   test_dir - Test directory where services are running
run-test-store-chunked-data test_dir:
    #!/usr/bin/env bash
    set -e
    echo "ğŸ§ª Running store chunked data test (test_dir: {{ test_dir }})..."
    node store_chunked_data.js

# Run store-big-data test only (services must already be running via start-services)
# Parameters:
#   test_dir - Test directory where services are running
run-test-store-big-data test_dir:
    #!/usr/bin/env bash
    set -e
    echo "ğŸ§ª Running store big data test (test_dir: {{ test_dir }})..."
    node store_big_data.js

# ============================================================================
# Standalone recipes (with full setup/teardown) - kept for local dev convenience
# ============================================================================

# Run authorize and store example with Docker IPFS
# Parameters:
#   mode - Connection mode: "ws" (WebSocket RPC node) or "smoldot" (light client)
#   runtime - Runtime name (e.g., "bulletin-polkadot-runtime", "bulletin-westend-runtime")
run-authorize-and-store runtime mode="ws": npm-install
    #!/usr/bin/env bash
    set -e

    if [ "{{ mode }}" = "smoldot" ]; then
        echo "ğŸš€ Starting authorize and store workflow test (mode: smoldot, runtime: {{ runtime }})..."
        SCRIPT_NAME="authorize_and_store_papi_smoldot.js"
    elif [ "{{ mode }}" = "ws" ]; then
        echo "ğŸš€ Starting authorize and store workflow test (mode: ws, runtime: {{ runtime }})..."
        SCRIPT_NAME="authorize_and_store_papi.js"
    else
        echo "âŒ Error: Invalid mode '{{ mode }}'. Must be 'ws' or 'smoldot'"
        exit 1
    fi

    TEST_DIR="$(just compute-test-dir)"
    just setup-services "$TEST_DIR" "{{ runtime }}"

    set +e
    # Run the script with chainspec_path parameter only for smoldot mode
    if [ "{{ mode }}" = "smoldot" ]; then
        # Set chainspec path based on runtime
        if [ "{{ runtime }}" = "bulletin-westend-runtime" ]; then
            # Parachain: relay chain (required) + parachain spec (optional)
            RELAY_CHAINSPEC_PATH="$TEST_DIR/bob/cfg/westend-local.json"
            PARACHAIN_CHAINSPEC_PATH="$TEST_DIR/bulletin-westend-collator-2/cfg/westend-local-1006.json"
            node $SCRIPT_NAME "$RELAY_CHAINSPEC_PATH" "$PARACHAIN_CHAINSPEC_PATH"
        else # bulletin-polkadot-runtime (solochain)
            CHAINSPEC_PATH="$TEST_DIR/bob/cfg/bulletin-polkadot-local.json"
            node $SCRIPT_NAME "$CHAINSPEC_PATH"
        fi
    else
        node $SCRIPT_NAME
    fi
    EXAMPLE_EXIT=$?
    
    echo ""
    echo "â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”"
    if [ $EXAMPLE_EXIT -eq 0 ]; then
        echo "âœ… Example completed successfully!"
    else
        echo "âŒ Example failed with exit code $EXAMPLE_EXIT"
    fi
    
    just teardown-services "$TEST_DIR"
    exit $EXAMPLE_EXIT

# Run store chunked data example with Docker IPFS
# Parameters:
#   runtime - Runtime name (e.g., "bulletin-polkadot-runtime", "bulletin-westend-runtime")
run-store-chunked-data runtime: npm-install
    #!/usr/bin/env bash
    set -e

    echo "ğŸš€ Starting store chunked data + DAG-PB workflow test (runtime: {{ runtime }})..."

    TEST_DIR="$(just compute-test-dir)"
    just setup-services "$TEST_DIR" "{{ runtime }}"

    set +e
    node store_chunked_data.js
    EXAMPLE_EXIT=$?

    echo ""
    echo "â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”"
    if [ $EXAMPLE_EXIT -eq 0 ]; then
        echo "âœ… Example completed successfully!"
    else
        echo "âŒ Example failed with exit code $EXAMPLE_EXIT"
    fi

    just teardown-services "$TEST_DIR"
    exit $EXAMPLE_EXIT

# Run store big data example.
run-store-big-data runtime: npm-install
    #!/usr/bin/env bash
    set -e

    echo "ğŸš€ Starting store big data workflow test ..."

    TEST_DIR="$(just compute-test-dir)"
    just setup-services "$TEST_DIR" "{{ runtime }}"

    set +e
    node store_big_data.js
    EXAMPLE_EXIT=$?

    echo ""
    echo "â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”"
    if [ $EXAMPLE_EXIT -eq 0 ]; then
        echo "âœ… Example completed successfully!"
    else
        echo "âŒ Example failed with exit code $EXAMPLE_EXIT"
    fi

    just teardown-services "$TEST_DIR"
    exit $EXAMPLE_EXIT

# ============================================================================
# Run tests against external environments
# ============================================================================

# Run authorize-and-store test against an external endpoint
# Parameters:
#   ws_url - WebSocket URL of the Bulletin chain node
#   seed - Account seed phrase or dev seed (e.g., "//Alice" or "word1 word2 ...")
run-tests-against ws_url seed: npm-install
    #!/usr/bin/env bash
    set -e
    echo "ğŸ§ª Running authorize-and-store test against external endpoint..."
    echo "   URL: {{ ws_url }}"
    just papi-generate "{{ ws_url }}"

    # TODO: connect to the local IPFS:8080
    # TODO: find out the WS/IP/ live node address

    # node authorize_and_store_papi.js "{{ ws_url }}" "{{ seed }}"
    node store_big_data.js "{{ ws_url }}" "{{ seed }}"

# Run authorize-and-store test against Westend
# Parameters:
#   seed - Account seed phrase or dev seed (e.g., "//Alice" or "word1 word2 ...")
run-tests-against-westend seed:
    just run-tests-against "wss://westend-bulletin-rpc.polkadot.io" "{{ seed }}"
